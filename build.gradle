plugins {
    id 'org.springframework.boot' version '2.3.1.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
    id "nu.studer.jooq" version "4.2"
}

apply from: "postgresql.gradle"

group = 'example.jooq'
version = '0.0.1-SNAPSHOT'

sourceCompatibility = "11"

repositories {
    mavenCentral()
}

dependencies {
    jooqRuntime dbDriverMaven
    implementation 'org.springframework.boot:spring-boot-starter-jooq'
    runtimeOnly dbDriverMaven
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

test {
    useJUnitPlatform()
}

jooq {
    sample(sourceSets.main) {
        jdbc {
            driver = dbDriverClass
            url = dbUrl
            user = dbUser
            password = dbPassword
            schema = dbSchema
        }
        generator {
            name = 'org.jooq.codegen.DefaultGenerator'
            strategy {
                name = 'org.jooq.codegen.DefaultGeneratorStrategy'
            }
            database {
                name = jooqDriverClass
                inputSchema = dbSchema
            }
            generate {
                relations = true
                deprecated = false
                records = true
                immutablePojos = true
                fluentSetters = true
                daos = true
            }
            target {
                packageName = 'example.jooq.generated'
            }
        }
    }
}
